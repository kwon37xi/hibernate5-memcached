buildscript {
    repositories {
        mavenCentral()
    }
}

ext {
    javaVersion = '1.8'
    hibernateVersion = '5.2.0.Final'
    lz4Version = '1.2.0'
    commonsLangVersion = '3.3.2'
    commonsCodecVersion = '1.9'
    kryoVersion = '2.24.0'
    kryoSerializersVersion = '0.26'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'kr.pe.kwonnam.hibernate5memcached'
    version = '0.8-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'

        testCompile 'junit:junit:4.11'
        testCompile 'org.assertj:assertj-core:3.5.1'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'ch.qos.logback:logback-classic:1.0.13' // DO NOT upgrade to 1.1.x. sometimes logs disappear.
    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jacoco {
        toolVersion = '0.7.1.201405082137'
    }

    test {
        jacoco {
            enabled = true
        }
    }
}

configure(subprojects.findAll { ['hibernate5-memcached-core', 'hibernate5-memcached-spymemcached-adapter'].contains(it.name) }) {
    apply plugin: 'maven'
    apply plugin: 'signing'

    // from http://central.sonatype.org/pages/gradle.html

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

	def uploadArchiveUsername = '--unknown username--'
	def uploadArchivePassword = '--unknown password--'

	if (project.hasProperty('ossrhUsername')) {
		uploadArchiveUsername = ossrhUsername
	}

	if (project.hasProperty('ossrhPassword')) {
		uploadArchivePassword = ossrhPassword
	}

	uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: uploadArchiveUsername, password: uploadArchivePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: uploadArchiveUsername, password: uploadArchivePassword)
                    authentication(userName: uploadArchiveUsername, password: uploadArchivePassword)
                }

                pom.project {
                    name "${project.name}"
                    packaging 'jar'
                    description 'hibernate5 memcached L2 cache implementation.'
                    url 'https://github.com/kwon37xi/hibernate5-memcached'

                    scm {
                        connection 'scm:git:https://github.com/kwon37xi/hibernate5-memcached.git'
                        url 'https://github.com/kwon37xi/hibernate5-memcached'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'kwon37xi'
                            name 'KwonNam Son'
                            email 'kwon37xi@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
